function(assert VAR)
  if(NOT ${VAR})
    message(FATAL_ERROR "\nAssertion failed: ${VAR} (value is '${${VAR}}')\n")
  endif()
  message(DEBUG "assert(${VAR}) passed (${VAR} = ${${VAR}})")
endfunction()

function(assert_defined VAR)
  if(NOT DEFINED ${VAR})
    message(FATAL_ERROR "\nAssertion failed: '${VAR}' is not defined be (value is '${${VAR}}')\n")
  endif()
  message(DEBUG "assert_defined(${VAR}) passed")
endfunction()

function(assert_unset VAR)
  if(${VAR})
    message(FATAL_ERROR "\nAssertion failed: '${VAR}' is set but should not be (value is '${${VAR}}')\n")
  endif()
  message(DEBUG "assert_unset(${VAR}) passed")
endfunction()

function(assert_file_exists FILENAME MESSAGE)
  if(NOT FILENAME)
    message(FATAL_ERROR "\nAssertion failed:  check for file existence, but filename (${FILENAME}) unset. Message: ${MESSAGE}\n")
  endif()
  if(NOT EXISTS ${FILENAME})
    message(FATAL_ERROR "\nAssertion failed:  file '${FILENAME}' does not exist.  Message: ${MESSAGE}\n")
  endif()
endfunction()

function(assert_is_test TEST)
  if(NOT TEST ${TEST})
    message(FATAL_ERROR "\nAssertion failed: `${TEST}` does not exist as is expected to.")
  else()
    message(DEBUG "assert_is_test(${TEST}) passed.")
  endif()
endfunction()

function(assert_is_fixture_setup TEST FIXTURE)
    get_test_property(${TEST} FIXTURES_SETUP FIXTURES_SETUP_VALUE)
    if(NOT FIXTURE IN_LIST FIXTURES_SETUP_VALUE)
        message(DEBUG "FIXTURES_SETUP property on ${TEST}: ${FIXTURES_SETUP_VALUE}")
        message(FATAL_ERROR "\nAssertion failed: `${TEST}` has not been configured as a setup test for the fixture `${FIXTURE}`")
    else()
        message(DEBUG "assert_is_fixture_setup(${TEST} ${FIXTURE}) passed.")
    endif()
endfunction()

function(assert_is_fixture_cleanup TEST FIXTURE)
    get_test_property(${TEST} FIXTURES_CLEANUP FIXTURES_CLEANUP_VALUE)
    if(NOT FIXTURE IN_LIST FIXTURES_CLEANUP_VALUE)
        message(DEBUG "FIXTURES_CLEANUP property on ${TEST}: ${FIXTURES_CLEANUP_VALUE}")
        message(FATAL_ERROR "\nAssertion failed: `${TEST}` has not been configured as a cleanup test for the fixture `${FIXTURE}`")
      elseif()
        message(DEBUG "assert_is_fixture_clearup(${TEST} ${FIXTURE}) passed.")
    endif()
endfunction()
